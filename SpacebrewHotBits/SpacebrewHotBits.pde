/* =================================================================
 * Spacebrew HotBits
 * Publishes Fourmilab's HotBits data to a Spacebrew server.  
 * =================================================================
 * https://www.fourmilab.ch/hotbits/
 * Genuine random numbers, generated by radioactive decay.
 * ------------------------------------------------------------------
 *
 * Try out with the public Sandbox at:
 * http://spacebrew.github.io/spacebrew/admin/admin.html?server=sandbox.spacebrew.cc
 * 1. Wire hot_bits with echo_bits
 * 2. Wire password with echo_string
 * 3. Click in the window to generate random password from a radioctive decay source
 *
 * Be careful with the HotBits 24-hour quota. :)
 *
 */
import spacebrew.*;

int bufferLen = 128;
int passwordLen = 8;
int publishRate = 30; // seconds

String server="sandbox.spacebrew.cc",
       name="P5 Hot Bits",
       description ="Publishes hot bits periodically and random passwords on demand.";

Spacebrew sb;
RandomBits rndBits;
RandomPassword rndPasswd;

int bitsRecv,
    bitsSent,
    rate;
String lastMsg = "-";

void setup() {
  
  size(320, 140);
  frameRate(30); 
  rate = (int) frameRate * publishRate;
  
  rndBits = new RandomBits(bufferLen);
  rndPasswd = new RandomPassword(passwordLen - 4);
  sb = new Spacebrew(this);
  
  sb.addPublish("hot_bits", "bits", bitsSent);
  sb.addPublish("password", "string");
  
  sb.addSubscribe("echo_bits", "bits");
  sb.addSubscribe("echo_string", "string");
  
  sb.connect(server, name, description);
  
}

void draw() {
  
  background(50);

  write("Sent", bitsSent, 40, #00FF00);
  write("Received", bitsRecv, 60, #FF0000);
  write("Echo", lastMsg, 80, #FFFFFF);
  
  if(frameCount == 1 || frameCount % rate == 0) {
    bitsSent = rndBits.nextBytes();
    sb.send("hot_bits", "bits", str(bitsSent));
  }
  
}

void mousePressed() {
  sb.send("password", "string", rndPasswd.genPassword());
}

void onStringMessage(String name, String msg){
  info("Got %s : %s", name, msg);
  lastMsg = msg;
}

void onCustomMessage(String name, String type, String num) {
  info("Got %s (%s) : %s", name, type, num);
  bitsRecv = int(num);
}

// ----------------------------------------------------------

private void write(String label, Object obj, int y, int c) {
  fill(#FFFFFF);
  text(label + ": ", 30, y);
  fill(c);
  text(obj.toString(), 180, y); 
}

private void info(String fmt, String...params) {
  String msg = String.format(fmt, params);
  println(msg);
}
